pkgname=linuxcnc
pkgver=2.8.0
pkgrel="1"
pkgdesc="A software system for computer control of machine tools."
arch=('i686' 'x86_64')
license=('GPL2')
curl="http://linuxcnc.org/"
depends=('bc' 'blt' 'bwidget' 'tcl' 'tk' 'xorg-server' 'python2-libgnome' 'python2-imaging' 'tkimg' 'python2-gtkglext' 'tclx' 'boost' 'boost-libs')
install=$pkgname.install
_commit="d860b30d78eb8764ca60eef51ce3870811edcbf6"
source=("https://github.com/LinuxCNC/linuxcnc/archive/${_commit}.tar.gz"
        "image-to-gcode.patch"
        "hm2_rpspi_mojo.patch")
md5sums=('3205da3a36a5833a15bbebdded8dd4ce'
         '6da7d023ad7f1328a39040884629716f'
         '06bed956363f6b0928584270b6545007')

makedepends=('git')

prepare() {
  cd $srcdir/$pkgname-$_commit/

  msg2 "Applying image-to-gcode patch..."
  patch -Np0 -i "${srcdir}/image-to-gcode.patch"

  msg2 "Applying hm2_rspi_mojo patch..."
  patch -Np0 -i "${srcdir}/hm2_rpspi_mojo.patch"

}

build () {
  find . -iname fixpaths.py -o -iname checkglade|xargs perl -p -i -e "s/python/python2/"
  cd $srcdir/$pkgname-$_commit/src
  pwd
  ./autogen.sh
  ./configure --with-realtime=uspace --with-python=/usr/bin/python2.7 --without-libusb-1.0 --without-libmodbus --prefix=/usr --enable-non-distributable=yes || return 1

  make || return 1
}

package() {
  cd $srcdir/$pkgname-$_commit/src
  make install DESTDIR=${pkgdir} || return 1

  # move tcl libs
  mv "${pkgdir}/usr/lib/tcltk" "${pkgdir}/usr/lib/tcl8.6"

  # install yapps for halcompile to work
  mkdir "${pkgdir}/usr/lib/python2.7/site-packages/yapps"
  cp -PR $srcdir/$pkgname-$_commit/lib/python/yapps/* "${pkgdir}/usr/lib/python2.7/site-packages/yapps/"
}

